buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
        classpath "org.spongepowered:mixingradle:0.7.+"
    }
}

plugins {
    id "net.minecraftforge.gradle" version "5.1.+"
    id "org.parchmentmc.librarian.forgegradle" version "1.+"
}

apply plugin: "org.spongepowered.mixin"
 
version = "forge-${rootProject.mc_version}-${rootProject.mod_version}"

sourceSets {
    main {
        java {
            srcDirs += "../common/src/main/java"
        }

        resources {
            srcDirs += "src/generated/resources"
            srcDirs += "../common/src/main/resources"
            srcDirs += "../common/src/generated/resources"
            exclude ".cache"
        }
    }
}

minecraft {
    mappings channel: "parchment", version: "${parchment_version}-${mc_version}"

    // accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        client {
            workingDirectory project.file("run")
            taskName "forge_client"

			property "mixin.env.remapRefMap", "true"
            property "mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg"
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            property "forge.enabledGameTestNamespaces", "kubejs_lcae"
            args "-mixin.config=kubejs_lcae.mixin.common.json"

            mods {
                kubejs_lcae {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")
            taskName "forge_server"

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            property "forge.enabledGameTestNamespaces", "kubejs_lcae"
            args "-mixin.config=kubejs_lcae.mixin.common.json"

            mods {
                kubejs_lcae {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file("run")
            taskName "forge_game_test_server"

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            property "forge.enabledGameTestNamespaces", "kubejs_lcae"

            mods {
                kubejs_lcae {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("run")
            taskName "forge_data"

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            args "--mod", "kubejs_lcae", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")

            mods {
                kubejs_lcae {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    // Mod
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_api_version}"
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    // Implementation
	implementation fg.deobf("dev.latvian.mods:kubejs-forge:${kubejs_version}")
	implementation fg.deobf("dev.latvian.mods:rhino-forge:${rhino_version}")
	implementation fg.deobf("dev.architectury:architectury-forge:${architectury_version}")

    // Runtiem Only
    runtimeOnly fg.deobf("curse.maven:the-one-probe-245211:${top_file}")
    runtimeOnly fg.deobf("curse.maven:jade-324717:${jade_file}")
}

mixin {
    add sourceSets.main, "kubejs_lcae.mixin.refmap.json"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "kubejs_lcae",
                "Specification-Vendor"    : "gisellevonbingen",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "gisellevonbingen",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "kubejs_lcae.mixin.common.json"
        ])
    }
}

jar.finalizedBy("reobfJar")
